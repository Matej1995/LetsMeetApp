plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version '1.8.10-1.0.9'
    id "com.google.protobuf" version "0.8.17"
}

kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

android {
    namespace 'cz.sandera.letsmeet'
    compileSdk 33

    defaultConfig {
        applicationId "cz.sandera.letsmeet"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        buildConfigField "String", "BASE_SERVER_URL", '"https://api.open-meteo.com/"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            kotlin.sourceSets {
                getByName(variant.name) {
                    kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.activity:activity-compose:$compose_activity_version"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_material_version"

    // Testing
    testImplementation "junit:junit:$test_unit_version"
    androidTestImplementation "androidx.test.ext:junit:$test_androidx_unit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    // Compose navigation - hilt
    implementation "androidx.hilt:hilt-navigation-compose:$compose_navigation_hilt"

    // Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_vesrion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_vesrion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"

    // Location Services
    implementation "com.google.android.gms:play-services-location:$google_service_location_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Splash screen
    implementation "androidx.core:core-splashscreen:$splash_screen_version"

    // Jetpack compose navigation
    implementation "io.github.raamcosta.compose-destinations:core:$compose_navigation_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_navigation_version"

    // Data store with protobuf
    implementation "androidx.datastore:datastore-preferences-core:$data_store_version"
    implementation "androidx.datastore:datastore:$data_store_version"
    // Starting from Protobuf 3.8.0, use the lite runtime library
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
}

protobuf {
    // Configures the Protobuf compilation and the protoc executable
    protoc {
        // Downloads from the repositories
        artifact = "com.google.protobuf:protoc:3.14.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // Configures the task output type
                java {
                    // Java Lite has smaller code size and is recommended for Android
                    option 'lite'
                }
            }
        }
    }
}